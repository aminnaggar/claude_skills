# ADR Skill Development

SKILL_DIR := "~/.claude/skills/aminnaggar_adrs"

# Default recipe - show available commands
default:
    @just --list

# Install skill to ~/.claude/skills/aminnaggar_adrs/
install:
    #!/bin/bash
    OLD_VERSION=""
    if [ -f {{SKILL_DIR}}/SKILL.md ]; then
        OLD_VERSION=$(awk '/^---$/{n++; if(n==2) exit; next} n==1{print}' {{SKILL_DIR}}/SKILL.md | yq -r '.version // ""')
    fi
    NEW_VERSION=$(awk '/^---$/{n++; if(n==2) exit; next} n==1{print}' src/SKILL.md | yq -r '.version // ""')
    mkdir -p {{SKILL_DIR}}/scripts
    cp -r src/* {{SKILL_DIR}}/
    if [ -n "$OLD_VERSION" ] && [ "$OLD_VERSION" != "$NEW_VERSION" ]; then
        echo "Updated from $OLD_VERSION to $NEW_VERSION"
    elif [ -n "$OLD_VERSION" ]; then
        echo "Reinstalled $NEW_VERSION"
    else
        echo "Installed $NEW_VERSION"
    fi

# Uninstall skill
uninstall:
    @if [ -d {{SKILL_DIR}} ]; then \
        rm -rf {{SKILL_DIR}} && echo "Uninstalled from {{SKILL_DIR}}"; \
    else \
        echo "Not installed at {{SKILL_DIR}}"; exit 1; \
    fi
